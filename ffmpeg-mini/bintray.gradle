apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = "1.2.0-beta"    //版本号，以后每次更新library都得更改
def siteUrl = 'https://github.com/AnJoiner/FFmpegCommand'    //Homepage URL of the library
def gitUrl = 'https://github.com/AnJoiner/FFmpegCommand.git'    //Git repository url
def issueUrl = 'https://github.com/AnJoiner/FFmpegCommand/issues'    //issue url of the library
def linksUrl = 'https://github.com/AnJoiner/FFmpegCommand/wiki'//links url of the

/*=====(路径可以是假的)======*/
group = "com.coder.command"    //组织
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                // Add your description here
                name 'core libary'
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'sourfeng' //your user ID
                        name 'LiPengfeng' //your name
                        email 'pengfeng161@gmail.com' //your email
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}
//生成源文件
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
//生成文档
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.encoding "UTF-8"
    options.charSet 'UTF-8'
    options.author true
    options.version true
    options.links linksUrl
    failOnError false
}
//文档打包成jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
//拷贝javadoc文件
task copyDoc(type: Copy) {
    from "${buildDir}/docs/"
    into "docs"
}

//上传到jcenter所需要的源码文件
artifacts {
    archives javadocJar
    archives sourcesJar
}
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "repositories"    //发布到Bintray的哪个仓库
        name = "ffmpeg-mini"    //发布到Bintray上的名字
        desc = "A FFmpeg library of Android"    //项目描述
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        issueTrackerUrl = issueUrl
        licenses = ["Apache-2.0"]
        labels = ['android']    //标签
        publish = true
        publicDownloadNumbers = true
    }
}

//上传bintray ：gradlew clean build bintrayUpload -PdryRun=false